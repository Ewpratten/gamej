plugins {
    id 'java-library'
}

repositories {
    jcenter()
}

dependencies {

}

sourceSets {
    main {
        java {
            srcDir "src"
            exclude "Template.java"
            exclude "UsageExample.java"
        }
    }
}

// Metadata for the jar file. This can be anything
jar {
    manifest {
        attributes(
            'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
            'Created-By'     : 'Gradle',
            'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
            'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}

// Automatic Javadoc
task('betterJavadoc', type: Javadoc) {
    source = project.sourceSets.main.allJava
    classpath = project.sourceSets.main.runtimeClasspath + project.sourceSets.main.compileClasspath

    // Set the JavaDoc webpage title
    title = "$project.name $project.version API"

    // These fix a JavaDoc bug
    options.addBooleanOption('-no-module-directories', true)
    options.author true

    // External references and options
    options.links = [
        'http://docs.spring.io/spring/docs/4.3.x/javadoc-api/', 
        'http://docs.oracle.com/javase/8/docs/api/', 
        'http://docs.spring.io/spring-ws/docs/2.3.0.RELEASE/api/', 
        'http://docs.spring.io/spring-security/site/docs/4.0.4.RELEASE/apidocs/'
    ]
    
    options.addStringOption 'Xdoclint:none'
    options.addStringOption '-quiet'
}

// Javadoc JAR
project.task('javadocJar', type: Jar) {
    from project.tasks.betterJavadoc
    classifier = 'javadoc'
}

project.tasks.assemble.dependsOn project.tasks.javadocJar

// SRCJar output
task('srcJar', type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

project.tasks.assemble.dependsOn project.tasks.srcJar